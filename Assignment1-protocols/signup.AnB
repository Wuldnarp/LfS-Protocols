Protocol: SignUp

Types:  Agent User, Website, mitID;
        Number NUser, NWebSite,NWebSite2, NmitID,NmitID2,NmitID3,NmitID4, Sid, NUserWebsite;
        Function pk, sk, pre;

Knowledge:
            User: User, Website, mitID, pk(User), inv(pk(User)), pk(mitID), sk(User,mitID), pre;
            Website: Website, User, mitID, pk(Website), inv(pk(Website)), pk(mitID), sk(Website,mitID), pre;
            mitID: mitID, Website, User, pk(mitID), inv(pk(mitID)), pk(Website), sk(User,mitID), sk(Website,mitID);

Actions:

#starts communication between User and Website
User -> Website: User,NUser,Sid

Website -> mitID: {|Website, User, NUser, NWebSite, Sid|}sk(Website,mitID)
mitID -> Website: {|NmitID|}sk(Website,mitID)
Website -> mitID: {pre(NmitID)}pk(mitID)

mitID -> User: {|NmitID2|}sk(User,mitID)
User -> mitID: {NmitID2}pk(mitID) 
mitID -> User: {|Website, User, NUser, NWebSite, Sid, NmitID3|}sk(User,mitID)

User -> mitID: {|Website, User, NUser, NWebSite, Sid, NmitID3, pk(User)|}sk(User,mitID)
mitID -> Website: {|Website, User, NUser, NWebSite, Sid, NmitID3, pk(User),NmitID4|}sk(Website,mitID)


Website -> User: {Website, User, NUser, NWebSite, Sid, NmitID3, NmitID4, NUserWebsite, pk(Website)}pk(User)
User -> Website: {pre(NUserWebsite)}pk(Website)


Goals:
        Website authenticates mitID on User
        User authenticates mitID on Website
        NUserWebsite secret between Website,User